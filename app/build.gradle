apply plugin: 'com.android.application'

/**
 * we use the number of commits as versionCode
 *
 * @link https://plus.google.com/u/0/+MarcReichelt/posts/BX2he4vSJaM
 */
def getNumberOfGitCommits() {
    return 'git rev-list --count HEAD'.execute().text.trim().toInteger()
}

/**
 * we make the git hash available in the app
 *
 * @link https://plus.google.com/u/0/+MarcReichelt/posts/BX2he4vSJaM
 */
def getCurrentGitHash() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.splitbrain.giraffe"
        minSdkVersion 11
        targetSdkVersion 25
        versionName "0.8.1"
        versionCode getNumberOfGitCommits()
        resValue "string", "app_git_hash", getCurrentGitHash()

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_5
            targetCompatibility JavaVersion.VERSION_1_5
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        targetCompatibility 1.6
        sourceCompatibility 1.6
    }
}
dependencies {
    testCompile 'junit:junit:4.12'

    //Essential google libraries
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
}

